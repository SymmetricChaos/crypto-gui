WHITESPACE =  { (" " | "⠀" | "\t") }
unknown    = !{ ANY }

basic_letter =  { "⠁" | "⠃" | "⠉" | "⠙" | "⠑" | "⠋" | "⠛" | "⠓" | "⠊" | "⠚" | "⠅" | "⠇" | "⠍" | "⠝" | "⠕" | "⠏" | "⠟" | "⠗" | "⠎" | "⠞" | "⠥" | "⠧" | "⠺" | "⠭" | "⠽" | "⠵" | "⠘⠝" | "⠸⠢" | "⠨⠁" | "⠨⠃" | "⠨⠛" | "⠨⠙" | "⠨⠑" | "⠨⠵" | "⠨⠱" | "⠨⠹" | "⠨⠊" | "⠨⠅" | "⠨⠇" | "⠨⠍" | "⠨⠝" | "⠨⠭" | "⠨⠕" | "⠨⠏" | "⠨⠗" | "⠨⠎" | "⠨⠞" | "⠨⠥" | "⠨⠋" | "⠨⠯" | "⠨⠽" | "⠨⠺" }
capitalize   =  { "⠠" }
diacritic    =  { "⠈⠡" | "⠈⠒" | "⠈⠬" | "⠈⠤" | "⠘⠯" | "⠘⠡" | "⠘⠩" | "⠘⠫" | "⠘⠻" | "⠘⠒" | "⠘⠌" | "⠘⠬" }
lower_letter = ${ diacritic* ~ basic_letter }
letter       = ${ capitalize? ~ diacritic* ~ basic_letter }

punctuation = { "⠂" | "⠆" | "⠒" | "⠲" | "⠖" | "⠦" | "⠘⠦" | "⠘⠴" | "⠠⠦" | "⠠⠴" | "⠸⠦" | "⠸⠴" | "⠠⠶" | "⠄" | "⠐⠣" | "⠐⠜" | "⠨⠣" | "⠨⠜" | "⠈⠣" | "⠈⠜" | "⠸⠣" | "⠸⠜" | "⠸⠌" | "⠸⠡" | "⠤" | "⠠⠤" | "⠐⠠⠤" | "⠨⠤" }

symbol = { "⠳⠕" | "⠳⠩" | "⠳⠪" | "⠳⠬" | "⠒" | "⠒⠒" | "⠶" | "⠶⠶" | "⠼⠡" | "⠼⠣" | "⠼⠩" | "⠈⠁" | "⠈⠉" | "⠈⠑" | "⠈⠋" | "⠈⠇" | "⠈⠝" | "⠈⠎" | "⠈⠽" | "⠈⠯" | "⠈⠣" | "⠈⠢" | "⠈⠔" | "⠈⠜" | "⠈⠠⠹" | "⠈⠠⠻" | "⠘⠉" | "⠘⠚" | "⠘⠏" | "⠘⠗" | "⠘⠎" | "⠘⠞" | "⠘⠭" | "⠘⠽" | "⠸⠹" | "⠸⠲" | "⠐⠂" | "⠐⠖" | "⠐⠶" | "⠐⠦" | "⠐⠔" | "⠐⠌" | "⠐⠤" | "⠨⠴" }

character = { letter | punctuation | symbol }

// simple standing alone = preceeded and followed by a space, hyphen, or dash (a cell may stand alone at the start or end of text)
// certain punctuation may go between the spacer and the objects standing alone, differing on the left and right
l_spacer            =  { "⠘⠦" | "⠠⠦" | "⠸⠦" | "⠠⠶" | "⠄" | "⠐⠣" | "⠨⠣" | "⠸⠣" }
r_spacer            =  { "⠂" | "⠆" | "⠒" | "⠲" | "⠖" | "⠦" | "⠘⠴" | "⠠⠴" | "⠸⠴" | "⠠⠶" | "⠄" | "⠐⠜" | "⠨⠜" | "⠸⠜" }
spacer              =  { "⠤" | "⠠⠤" | "⠐⠠⠤" }
alphabetic_wordsign =  { "⠃" | "⠉" | "⠙" | "⠑" | "⠋" | "⠛" | "⠓" | "⠚" | "⠅" | "⠇" | "⠍" | "⠝" | "⠏" | "⠟" | "⠗" | "⠎" | "⠞" | "⠥" | "⠧" | "⠭" | "⠽" | "⠵" | "⠺" }
alpha_ws_alone      =  { (WHITESPACE | spacer | SOI) ~ ( l_spacer* ~ alphabetic_wordsign ~ r_spacer* ~ (WHITESPACE | spacer | EOI))+ }

// The numeric_symbols are the digits, the digits preceeded by a numeric space, the period, comma, and the fraction bar.
numeric_symbol   =  { "⠁" | "⠃" | "⠉" | "⠙" | "⠑" | "⠋" | "⠛" | "⠓" | "⠊" | "⠚" | "⠐⠁" |"⠐⠃"| "⠐⠉"|"⠐⠙"| "⠐⠑"| "⠐⠋" | "⠐⠛"|"⠐⠓"|"⠐⠊" | "⠐⠚" | "⠂" | "⠲" | "⠌" }
numeric_sequence = ${ "⠼" ~ WHITESPACE* ~ numeric_symbol+ }

numeric_passage = ${ "⠼⠼" ~ numeric_symbol+ ~ "⠼⠄"}

capital_sequence = ${ "⠠⠠" ~ lower_letter+ ~ (&"⠠" | &WHITESPACE | "⠠⠄")? }
capital_passage  = ${ "⠠⠠⠠" ~ (lower_letter | punctuation | symbol | numeric_sequence)+ ~ (WHITESPACE+ ~ (lower_letter | punctuation | symbol | numeric_sequence)+)+ ~ "⠠⠄" }

g1_passage = ${ ( capital_passage | capital_sequence | numeric_sequence | character | WHITESPACE | unknown )+ }
g2_passage = ${ ( alpha_ws_alone | capital_passage | capital_sequence | numeric_sequence | character | WHITESPACE | unknown )+ }

// braille_cell    =  { "⠁" | "⠃" | "⠉" | "⠙" | "⠑" | "⠋" | "⠛" | "⠓" | "⠊" | "⠚" | "⠅" | "⠇" | "⠍" | "⠝" | "⠕" | "⠏" | "⠟" | "⠗" | "⠎" | "⠞" | "⠥" | "⠧" | "⠭" | "⠽" | "⠵" | "⠯" | "⠿" | "⠷" | "⠮" | "⠾" | "⠡" | "⠣" | "⠩" | "⠹" | "⠱" | "⠫" | "⠻" | "⠳" | "⠪" | "⠺" | "⠂" | "⠆" | "⠒" | "⠲" | "⠢" | "⠖" | "⠶" | "⠦" | "⠔" | "⠴" | "⠌" | "⠬" | "⠼" | "⠜" | "⠄" | "⠤" | "⠈" | "⠘" | "⠸" | "⠐" | "⠨" | "⠰" | "⠠"}
